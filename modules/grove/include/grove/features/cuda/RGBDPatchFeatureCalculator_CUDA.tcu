/**
 * grove: RGBDPatchFeatureCalculator_CUDA.tcu
 * Copyright (c) Torr Vision Group, University of Oxford, 2017. All rights reserved.
 */

#include "features/cuda/RGBDPatchFeatureCalculator_CUDA.h"

#include "features/shared/RGBDPatchFeatureCalculator_Shared.h"

namespace grove {

namespace {

//#################### CUDA KERNELS ####################

template <typename KeypointType, typename DescriptorType>
__global__ void ck_compute_colour_feature(const KeypointType *keypoints, DescriptorType *descriptors, const Vector4u *rgb, const float *depth,
                                          const Vector4i *offsetsRgb, const uchar *channelsRgb, Vector2i imgSize, Vector2i outSize,
                                          uint32_t featureStep, bool normalise, uint32_t featuresCount, uint32_t outputFeaturesOffset)
{
  // Determine the coordinates of the pixel in the descriptors image into which we will write the colour feature.
  const Vector2i xyOut(threadIdx.x + blockIdx.x * blockDim.x, threadIdx.y + blockIdx.y * blockDim.y);

  if(xyOut.x < outSize.width && xyOut.y < outSize.height)
  {
    // Determine the coordinates of the pixel in the RGBD image for which we are computing the feature descriptor.
    const Vector2i xyIn = xyOut * featureStep;

    // Compute the colour feature for the specified RGBD pixel and write it into the correct place in the pixel's descriptor.
    compute_colour_patch_feature(keypoints, descriptors, rgb, depth, offsetsRgb, channelsRgb, imgSize, outSize, normalise, xyIn, xyOut, featuresCount, outputFeaturesOffset);
  }
}

template <typename KeypointType, typename DescriptorType>
__global__ void ck_compute_depth_feature(const KeypointType *keypoints, DescriptorType *descriptors, const float *depth, const Vector4i *offsetsDepth,
                                         Vector2i imgSize, Vector2i outSize, Vector4f intrinsics, Matrix4f cameraPose, uint32_t featureStep,
                                         bool normalise, uint32_t featuresCount, uint32_t outputFeaturesOffset)
{
  // Determine the coordinates of the pixel in the descriptors image into which we will write the depth feature.
  const Vector2i xyOut(threadIdx.x + blockIdx.x * blockDim.x, threadIdx.y + blockIdx.y * blockDim.y);

  if(xyOut.x < outSize.width && xyOut.y < outSize.height)
  {
    // Determine the coordinates of the pixel in the RGBD image for which we are computing the feature descriptor.
    const Vector2i xyIn(xyOut.x * featureStep, xyOut.y * featureStep);

    // Compute the depth feature for the specified RGBD pixel and write it into the correct place in the pixel's descriptor.
    compute_depth_patch_feature(keypoints, descriptors, depth, offsetsDepth, imgSize, outSize, intrinsics, cameraPose, normalise, xyIn, xyOut, featuresCount, outputFeaturesOffset);
  }
}

template <typename KeypointType>
__global__ void ck_compute_keypoints(const Vector2i inSize, const Vector2i outSize, const Vector4u *rgb, const float *depths,
                                     const Matrix4f cameraPose, const Vector4f intrinsics, const uint32_t featureStep,
                                     KeypointType *keypoints)
{
  // Determine the coordinates of the pixel in the keypoints image into which we will write the keypoint.
  const Vector2i xyOut(threadIdx.x + blockIdx.x * blockDim.x, threadIdx.y + blockIdx.y * blockDim.y);

  if(xyOut.x < outSize.width && xyOut.y < outSize.height)
  {
    // Determine the coordinates of the pixel in the RGBD image for which we are computing the keypoint.
    const Vector2i xyIn = xyOut * featureStep;

    // Compute the keypoint for the specified RGBD pixel and write it into the keypoints image.
    compute_keypoint(xyIn, xyOut, inSize, outSize, rgb, depths, cameraPose, intrinsics, keypoints);
  }
}

}

//#################### CONSTRUCTORS ####################

template <typename KeypointType, typename DescriptorType>
RGBDPatchFeatureCalculator_CUDA<KeypointType,DescriptorType>::RGBDPatchFeatureCalculator_CUDA(bool depthAdaptive, uint32_t depthFeatureCount, uint32_t depthFeatureOffset,
                                                                                              uint32_t rgbFeatureCount, uint32_t rgbFeatureOffset)
: RGBDPatchFeatureCalculator<KeypointType,DescriptorType>(depthAdaptive, depthFeatureCount, depthFeatureOffset, rgbFeatureCount, rgbFeatureOffset)
{
  // Copy the memory blocks used to specify the features across to the GPU.
  this->m_depthOffsets->UpdateDeviceFromHost();
  this->m_rgbChannels->UpdateDeviceFromHost();
  this->m_rgbOffsets->UpdateDeviceFromHost();
}

//#################### PUBLIC MEMBER FUNCTIONS ####################

template <typename KeypointType, typename DescriptorType>
void RGBDPatchFeatureCalculator_CUDA<KeypointType,DescriptorType>::compute_feature(const ITMUChar4Image *rgbImage, const ITMFloatImage *depthImage,
                                                                                   const Matrix4f& cameraPose, const Vector4f& intrinsics,
                                                                                   KeypointsImage *keypointsImage, DescriptorsImage *descriptorsImage) const
{
  // Check that the input images are valid.
  this->check_input_images(rgbImage, depthImage);

  // Ensure the output images are the right size (always a no-op after the first time).
  // They should have one pixel for each element of the sampling grid.
  const Vector2i outSize = depthImage->noDims / this->m_featureStep;
  keypointsImage->ChangeDims(outSize);
  descriptorsImage->ChangeDims(outSize);

  const Vector4i *depthOffsets = this->m_depthOffsets->GetData(MEMORYDEVICE_CUDA);
  const float *depths = depthImage->GetData(MEMORYDEVICE_CUDA);
  const Vector2i inSize = depthImage->noDims;
  const Vector4u *rgb = rgbImage->GetData(MEMORYDEVICE_CUDA);
  const uchar *rgbChannels = this->m_rgbChannels->GetData(MEMORYDEVICE_CUDA);
  const Vector4i *rgbOffsets = this->m_rgbOffsets->GetData(MEMORYDEVICE_CUDA);

  KeypointType *keypoints = keypointsImage->GetData(MEMORYDEVICE_CUDA);
  DescriptorType *descriptors = descriptorsImage->GetData(MEMORYDEVICE_CUDA);

  dim3 blockSize(32, 32);
  dim3 gridSize((outSize.x + blockSize.x - 1) / blockSize.x, (outSize.y + blockSize.y - 1) / blockSize.y);

  // Compute the keypoint for each pixel in the RGBD image.
  ck_compute_keypoints<<<gridSize,blockSize>>>(inSize, outSize, rgb, depths, cameraPose, intrinsics, this->m_featureStep, keypoints);
  ORcudaKernelCheck;

  // If there is a depth image available and any depth features need to be computed, compute them for each keypoint.
  if(depths && this->m_depthFeatureCount > 0)
  {
    ck_compute_depth_feature<<<gridSize,blockSize>>>(
      keypoints, descriptors, depths, depthOffsets, inSize, outSize,
      intrinsics, cameraPose, this->m_featureStep, this->m_normaliseDepth,
      this->m_depthFeatureCount, this->m_depthFeatureOffset
    );
    ORcudaKernelCheck;
  }

  // If there is a colour image available and any colour features need to be computed, compute them for each keypoint.
  if(rgb && this->m_rgbFeatureCount > 0)
  {
    ck_compute_colour_feature<<<gridSize,blockSize>>>(
      keypoints, descriptors, rgb, depths, rgbOffsets, rgbChannels,
      inSize, outSize, this->m_featureStep, this->m_normaliseRgb,
      this->m_rgbFeatureCount, this->m_rgbFeatureOffset
    );
    ORcudaKernelCheck;
  }
}

}

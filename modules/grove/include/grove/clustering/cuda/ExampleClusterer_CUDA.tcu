/**
 * grove: ExampleClusterer_CUDA.cu
 * Copyright (c) Torr Vision Group, University of Oxford, 2016. All rights reserved.
 */

#include "ExampleClusterer_CUDA.h"

#include <iostream>

#include "../shared/ExampleClusterer_Shared.h"

namespace grove {

__global__ void ck_reset_temporaries(int *clustersPerReservoir,
    int *clusterSizes, int *clusterSizesHistogram, int reservoirCapacity)
{
  // This is launched with 1 block, 1 thread per reservoir.
  // No need to check if reservoirIdx is within bounds. It always is.
  const int reservoirIdx = threadIdx.x;

  example_clusterer_reset_temporaries(clustersPerReservoir, clusterSizes, clusterSizesHistogram,
                                      reservoirIdx, reservoirCapacity);
}

template <typename ClusterType>
__global__ void ck_reset_predictions(ClusterType *predictions)
{
  const int predictionIdx = threadIdx.x;
  example_clusterer_reset_predictions(predictions, predictionIdx);
}

template <typename ExampleType>
__global__ void ck_compute_density(const ExampleType *examples,
    const int *reservoirSizes, float *densities, int reservoirCapacity, float sigma)
{
  // The assumption is that the kernel indices are always valid.
  // 1 block per reservoir, 1 thread per element
  const int reservoirIdx = blockIdx.x;
  const int elementIdx = threadIdx.x;

  example_clusterer_compute_density(examples, reservoirSizes, densities,
                                    reservoirCapacity, reservoirIdx, elementIdx, sigma);
}

template <typename ExampleType>
__global__ void ck_link_neighbors(const ExampleType *examples,
    const int *reservoirSizes, const float *densities, int *parents,
    int *clusterIndices, int *nbClustersPerReservoir, int reservoirCapacity, float tauSq)
{
  // The assumption is that the kernel indices are always valid.
  // 1 block per reservoir, 1 thread per element
  const int reservoirIdx = blockIdx.x;
  const int elementIdx = threadIdx.x;

  example_clusterer_link_neighbors(examples, reservoirSizes, densities, parents,
                                   clusterIndices, nbClustersPerReservoir, reservoirCapacity,
                                   reservoirIdx, elementIdx, tauSq);
}

__global__ void ck_identify_clusters(const int *parents, int *clusterIndices, int *clusterSizes,
    int reservoirCapacity)
{
  // The assumption is that the kernel indices are always valid.
  // 1 block per reservoir, 1 thread per element
  const int reservoirIdx = blockIdx.x;
  const int elementIdx = threadIdx.x;

  example_clusterer_identify_clusters(parents, clusterIndices, clusterSizes,
                                      reservoirCapacity, reservoirIdx, elementIdx);
}

__global__ void ck_compute_cluster_histogram(const int *clusterSizes,
    const int *nbClustersPerReservoir, int *clusterSizesHistogram,
    int reservoirCapacity)
{
  // The assumption is that the block indices are always valid.
  const int reservoirIdx = blockIdx.x;
  const int clusterIdx = threadIdx.x;

  example_clusterer_compute_cluster_histogram(clusterSizes, nbClustersPerReservoir, clusterSizesHistogram,
                                              reservoirCapacity, reservoirIdx, clusterIdx);
}

__global__ void ck_select_clusters(const int *clusterSizes,
    const int *clusterSizesHistogram, const int *nbClustersPerReservoir,
    int *selectedClusters, int reservoirCapacity,
    int maxSelectedClusters, int minClusterSize)
{
  // The assumption is that the kernel indices are always valid.
  // "Sequential kernel": only one block is launched
  const int reservoirIdx = threadIdx.x;

  example_clusterer_select_clusters(clusterSizes, clusterSizesHistogram, nbClustersPerReservoir,
                                    selectedClusters, reservoirCapacity, maxSelectedClusters,
                                    minClusterSize, reservoirIdx);
}

// TODO: this will need specialization for 2D keypoints without colour.
template <typename ExampleType, typename ClusterType>
__global__ void ck_compute_modes(const ExampleType *examples,
    const int *reservoirSizes, const int *clusterIndices,
    const int *selectedClusters, ClusterType *predictions,
    int reservoirCapacity, int maxSelectedClusters)
{
  // One thread per cluster, one block per reservoir
  const int reservoirIdx = blockIdx.x;
  const int clusterIdx = threadIdx.x;

  example_clusterer_compute_modes(examples, reservoirSizes, clusterIndices,
                                  selectedClusters, predictions, reservoirCapacity,
                                  maxSelectedClusters, reservoirIdx, clusterIdx);
}

template <typename ExampleType, typename ClusterType>
ExampleClusterer_CUDA<ExampleType, ClusterType>::ExampleClusterer_CUDA(float sigma, float tau, uint32_t maxClusterCount, uint32_t minClusterSize)
  : ExampleClusterer<ExampleType, ClusterType>(sigma, tau, maxClusterCount, minClusterSize)
{}

template <typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::find_modes(
    const ExampleImage_CPtr &exampleReservoirs,
    const ITMIntMemoryBlock_CPtr &keypointReservoirsSize,
    ClusterBlock_Ptr &predictions,
    uint32_t startIdx, uint32_t count)
{
  const uint32_t nbReservoirs = exampleReservoirs->noDims.height;
  const uint32_t reservoirCapacity = exampleReservoirs->noDims.width;

  if (startIdx + count > nbReservoirs)
    throw std::runtime_error("startIdx + count > nbReservoirs");

  const Vector2i temporariesSize(reservoirCapacity, count);

  if(this->m_densities->noDims.x < temporariesSize.x || this->m_densities->noDims.y < temporariesSize.y)
  {
    // Happens only once
    this->m_densities->ChangeDims(temporariesSize);
    this->m_parents->ChangeDims(temporariesSize);
    this->m_clusterIdx->ChangeDims(temporariesSize);
    this->m_clusterSizes->ChangeDims(temporariesSize);
    this->m_clusterSizesHistogram->ChangeDims(temporariesSize);

    this->m_selectedClusters->ChangeDims(Vector2i(this->m_maxClusterCount, count));
    this->m_nbClustersPerReservoir->ChangeDims(count);
  }

  // Grab a pointer to the beginning of the first reservoir of interest (having index startIdx).
  const ExampleType *examples = exampleReservoirs->GetData(MEMORYDEVICE_CUDA) + startIdx * reservoirCapacity;
  // Grab a pointer to the size of the first reservoir of interest.
  const int *reservoirSizes = keypointReservoirsSize->GetData(MEMORYDEVICE_CUDA) + startIdx;
  // Grab a pointer to the first prediction to compute.
  ClusterType *predictionsData = predictions->GetData(MEMORYDEVICE_CUDA) + startIdx;

  float *densities = this->m_densities->GetData(MEMORYDEVICE_CUDA);

  dim3 blockSize(reservoirCapacity); // One thread per item in each reservoir
  dim3 gridSize(count); // One block per reservoir to process

  int *nbClustersPerReservoir = this->m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizes = this->m_clusterSizes->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizesHistogram = this->m_clusterSizesHistogram->GetData(MEMORYDEVICE_CUDA);

  // 1 single block, 1 thread per reservoir
  ck_reset_temporaries<<<1, gridSize>>>(nbClustersPerReservoir, clusterSizes,
      clusterSizesHistogram, reservoirCapacity);
  ORcudaKernelCheck;

  ck_reset_predictions<<<1, gridSize>>>(predictionsData);
  ORcudaKernelCheck;

  ck_compute_density<<<gridSize, blockSize>>>(examples, reservoirSizes, densities, reservoirCapacity, this->m_sigma);
  ORcudaKernelCheck;

  int *parents = this->m_parents->GetData(MEMORYDEVICE_CUDA);
  int *clusterIndices = this->m_clusterIdx->GetData(MEMORYDEVICE_CUDA);

  ck_link_neighbors<<<gridSize, blockSize>>>(examples, reservoirSizes, densities, parents, clusterIndices,
      nbClustersPerReservoir, reservoirCapacity, this->m_tau * this->m_tau);
  ORcudaKernelCheck;

  ck_identify_clusters<<<gridSize, blockSize>>>(parents, clusterIndices, clusterSizes, reservoirCapacity);
  ORcudaKernelCheck;

  ck_compute_cluster_histogram<<<gridSize, blockSize>>>(clusterSizes, nbClustersPerReservoir,
      clusterSizesHistogram, reservoirCapacity);
  ORcudaKernelCheck;

  int *selectedClusters = this->m_selectedClusters->GetData(MEMORYDEVICE_CUDA);
  // 1 single block, 1 thread per reservoir
  ck_select_clusters<<<1, gridSize>>>(clusterSizes, clusterSizesHistogram,
      nbClustersPerReservoir, selectedClusters, reservoirCapacity,
      this->m_maxClusterCount, this->m_minClusterSize);
  ORcudaKernelCheck;

  ck_compute_modes<<<gridSize, this->m_maxClusterCount>>>(examples, reservoirSizes, clusterIndices, selectedClusters,
      predictionsData, reservoirCapacity, this->m_maxClusterCount);
  ORcudaKernelCheck;

//  m_nbClustersPerReservoir->UpdateHostFromDevice();
//  m_clusterSizes->UpdateHostFromDevice();
//  reservoirs->get_reservoirs_size()->UpdateHostFromDevice();
//
//  for (int i = 0; i < count; ++i)
//  {
//    std::cout << "Reservoir " << i + startIdx << " has "
//        << m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CPU)[i + startIdx]
//        << " clusters and "
//        << reservoirs->get_reservoirs_size()->GetData(MEMORYDEVICE_CPU)[i
//            + startIdx] << " elements." << std::endl;
//    for (int j = 0;
//        j < m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CPU)[i + startIdx];
//        ++j)
//    {
//      std::cout << "\tCluster " << j << ": "
//          << m_clusterSizes->GetData(MEMORYDEVICE_CPU)[(i + startIdx)
//              * reservoirCapacity + j] << " elements." << std::endl;
//    }
  //  }
}

template<typename ExampleType, typename ClusterType>
const ExampleType *ExampleClusterer_CUDA<ExampleType, ClusterType>::get_pointer_to_reservoir(const ExampleImage_CPtr &exampleReservoirs, uint32_t reservoirIdx) const
{
  const int reservoirCapacity = exampleReservoirs->noDims.width;
  return exampleReservoirs->GetData(MEMORYDEVICE_CUDA) + reservoirIdx * reservoirCapacity;
}

template<typename ExampleType, typename ClusterType>
const int *ExampleClusterer_CUDA<ExampleType, ClusterType>::get_pointer_to_reservoir_size(const ITMIntMemoryBlock_CPtr &exampleReservoirsSize, uint32_t reservoirIdx) const
{
  return exampleReservoirsSize->GetData(MEMORYDEVICE_CUDA) + reservoirIdx;
}

template<typename ExampleType, typename ClusterType>
ClusterType *ExampleClusterer_CUDA<ExampleType, ClusterType>::get_pointer_to_prediction(const ClusterBlock_Ptr &predictions, uint32_t predictionIdx) const
{
  return predictions->GetData(MEMORYDEVICE_CUDA) + predictionIdx;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::reset_temporaries(uint32_t reservoirCapacity, uint32_t reservoirCount)
{
  dim3 gridSize(reservoirCount); // One block per reservoir to process

  int *nbClustersPerReservoir = this->m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizes = this->m_clusterSizes->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizesHistogram = this->m_clusterSizesHistogram->GetData(MEMORYDEVICE_CUDA);

  // 1 single block, 1 thread per reservoir
  ck_reset_temporaries<<<1, gridSize>>>(nbClustersPerReservoir, clusterSizes, clusterSizesHistogram, reservoirCapacity);
  ORcudaKernelCheck;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::compute_modes(const ExampleType *examples, const int *reservoirSizes, ClusterType *predictionsData, uint32_t maxClusterCount, uint32_t reservoirCapacity, uint32_t reservoirCount)
{
  dim3 gridSize(reservoirCount); // One block per reservoir to process
  dim3 blockSize(maxClusterCount); // One thread per cluster

  int *nbClustersPerReservoir = this->m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizes = this->m_clusterSizes->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizesHistogram = this->m_clusterSizesHistogram->GetData(MEMORYDEVICE_CUDA);

  int *parents = this->m_parents->GetData(MEMORYDEVICE_CUDA);
  int *clusterIndices = this->m_clusterIdx->GetData(MEMORYDEVICE_CUDA);

  int *selectedClusters = this->m_selectedClusters->GetData(MEMORYDEVICE_CUDA);

  // 1 block per reservoir, 1 thread per cluster
  ck_compute_modes<<<gridSize, blockSize>>>(examples, reservoirSizes, clusterIndices, selectedClusters, predictionsData, reservoirCapacity, maxClusterCount);
  ORcudaKernelCheck;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::select_clusters(uint32_t maxClusterCount, uint32_t minClusterSize, uint32_t reservoirCapacity, uint32_t reservoirCount)
{
  int *nbClustersPerReservoir = this->m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizes = this->m_clusterSizes->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizesHistogram = this->m_clusterSizesHistogram->GetData(MEMORYDEVICE_CUDA);
  int *selectedClusters = this->m_selectedClusters->GetData(MEMORYDEVICE_CUDA);

  dim3 blockSize(reservoirCount); // One thread per reservoir
  ck_select_clusters<<<1, blockSize>>>(clusterSizes, clusterSizesHistogram, nbClustersPerReservoir, selectedClusters, reservoirCapacity, maxClusterCount, minClusterSize);
  ORcudaKernelCheck;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::compute_cluster_size_histograms(uint32_t reservoirCapacity, uint32_t reservoirCount)
{
  dim3 blockSize(reservoirCapacity); // One thread per item in each reservoir
  dim3 gridSize(reservoirCount); // One block per reservoir to process

  int *nbClustersPerReservoir = this->m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizes = this->m_clusterSizes->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizesHistogram = this->m_clusterSizesHistogram->GetData(MEMORYDEVICE_CUDA);

  ck_compute_cluster_histogram<<<gridSize, blockSize>>>(clusterSizes, nbClustersPerReservoir, clusterSizesHistogram, reservoirCapacity);
  ORcudaKernelCheck;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::identify_clusters(uint32_t reservoirCapacity, uint32_t reservoirCount)
{
  dim3 blockSize(reservoirCapacity); // One thread per item in each reservoir
  dim3 gridSize(reservoirCount); // One block per reservoir to process

  int *clusterIndices = this->m_clusterIdx->GetData(MEMORYDEVICE_CUDA);
  int *clusterSizes = this->m_clusterSizes->GetData(MEMORYDEVICE_CUDA);
  int *parents = this->m_parents->GetData(MEMORYDEVICE_CUDA);

  ck_identify_clusters<<<gridSize, blockSize>>>(parents, clusterIndices, clusterSizes, reservoirCapacity);
  ORcudaKernelCheck;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::link_neighbors(const ExampleType *examples, const int *reservoirSizes, uint32_t reservoirCapacity, uint32_t reservoirCount, float tauSq)
{
  dim3 blockSize(reservoirCapacity); // One thread per item in each reservoir
  dim3 gridSize(reservoirCount); // One block per reservoir to process

  const float *densities = this->m_densities->GetData(MEMORYDEVICE_CUDA);
  int *nbClustersPerReservoir = this->m_nbClustersPerReservoir->GetData(MEMORYDEVICE_CUDA);
  int *parents = this->m_parents->GetData(MEMORYDEVICE_CUDA);
  int *clusterIndices = this->m_clusterIdx->GetData(MEMORYDEVICE_CUDA);

  ck_link_neighbors<<<gridSize, blockSize>>>(examples, reservoirSizes, densities, parents, clusterIndices,
      nbClustersPerReservoir, reservoirCapacity, tauSq);
  ORcudaKernelCheck;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::compute_density(const ExampleType *examples, const int *reservoirSizes, uint32_t reservoirCapacity, uint32_t reservoirCount, float sigma)
{
  float *densities = this->m_densities->GetData(MEMORYDEVICE_CUDA);

  dim3 blockSize(reservoirCapacity); // One thread per item in each reservoir
  dim3 gridSize(reservoirCount); // One block per reservoir to process

  ck_compute_density<<<gridSize, blockSize>>>(examples, reservoirSizes, densities, reservoirCapacity, sigma);
  ORcudaKernelCheck;
}

template<typename ExampleType, typename ClusterType>
void ExampleClusterer_CUDA<ExampleType, ClusterType>::reset_predictions(ClusterType *predictionsData, uint32_t reservoirCount) const
{
  dim3 gridSize(reservoirCount); // One block per reservoir to process

  ck_reset_predictions<<<1, gridSize>>>(predictionsData);
  ORcudaKernelCheck;
}

}
